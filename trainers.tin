#nop This script tracks trainers your character has run across for
#nop spells, feats, skills, weapons, languages, or level.

#nop Usage:
#nop `trainers` will display a list of all trainables known.
#nop `trainers [skill/spell/feat/weapon/language/level]` will display
#nop NPCs the character has met who can train the specified item, and
#nop their rates. The trainable must be specified in full 
#nop (i.e. `trainers long axes`).

#nop The script saves known trainers in separate files for each character.
#nop This avoids multiplay by letting you track what each character knows
#nop separately.
#showme {Loading trainers from ${charname}_skills.tin};
#class {trainers} {open};
#var trainers {0};
#class {trainers} {read} {${charname}_skills.tin};
#class {trainers} {open};

#nop This underlines skills, feats, spells, weapons, and languages in the
#nop appropriate lists in game if your character knows of a trainer.
#FUNCTION {highlightskill}
{
    #if {"$trainers[%1][type]" == "spell"} 
    {
        #format target {%-20s} {%1};
        #format target {%.20s} {$target};
        #format target {%l} {$target};
        #highlight {{?<=    }$target{?= [\( ]}} {underscore};
    };
    #if {"$trainers[%1][type]" == "feat"} 
    {
        #format target {%-33s} {%1};
        #format target {%.33s} {$target};
        #format target {%l} {$target};
        #highlight {$target{?= [\-\*]}} {underscore};
    };
    #if {"$trainers[%1][type]" == "skill" || "$trainers[%1][type]" == "weapon" || "$trainers[%1][type]" == "language"} 
    {
        #format target {%-14s} {%1};
        #format target {%.14s} {$target};
        #format target {%l} {$target};
        #highlight {$target{?= \([CIWLDS]}} {underscore};
    };
    #return 0;
}

#foreach {$trainers[]} {skill}
{
	#if {"$trainers[$skill][type]" != "level" && "$trainers[$skill][type]" != "statistic" } 
    {
    	#var result @highlightskill{$skill};
    }
}

#ACTION {%1 says, 'I can train:}
{
	#variable active_trainer {%1}
}
{5};

#ACTION {~      \e[0;36;40m%* \e[1;37;40mfor %*.}
{	
	#variable color {\e[0\;36\;40m};
	#var result @add_skill{$color; %1; %2; skill};
}
{5};

#ACTION {~      \e[1;34;40m%* \e[1;37;40mfor %*.}
{	
	#variable color {\e[1\;34\;40m};
	#var result @add_skill{$color; %1; %2; spell};
}
{5};

#ACTION {~      \e[1;35;40m%* \e[1;37;40mfor %*.}
{	
	#variable color {\e[1\;35\;40m};
	#var result @add_skill{$color; %1; %2; language};
}
{5};

#ACTION {~      \e[1;33;40m%* \e[1;37;40mfor %*.}
{	
	#variable color {\e[0\;33\;40m};
	#var result @add_skill{$color; %1; %2; feat};
}
{5};

#ACTION {~      \e[0;37;40m%* \e[1;37;40mfor %*.}
{	
	#variable color {\e[0\;37\;40m};
	#var result @add_skill{$color; %1; %2; weapon};
}
{5};

#ACTION {~      \e[1;37;40m%* \e[1;37;40mfor %*.}
{	
	#variable color {\e[1\;37\;40m};
	#var result @add_skill{$color; %1; %2; level};
}
{5};


#FUNCTION {add_skill}
{
	#format skill {%p} {%2};
	#format cost {%p} {%3};
	#variable {trainers[$skill][type]} {%4};
    #variable {trainers[$skill][teachers][$active_trainer]} {%3};
	#variable {trainers[$skill][color]} {%1};
	#if {"$trainers[$skill][type]" != "level" && "$trainers[$skill][type]" != "statistic" } 
    {
    	#var result @highlightskill{$skill};
    };
	#nop #echo {$active_trainer teaches %4 %1$skill for $cost};
	#variable result {@write_trainers{}}
};

#FUNCTION {write_trainers}
{
	#config log raw;
	#system {rm -f ${charname}_skills.tin};
	#foreach {$trainers[]} {skill}
	{
		#line log {${charname}_skills.tin} {#VARIABLE \x7btrainers[$skill]\x7d \x7b$trainers[$skill]\x7d}
	};
	#config log html;
}

#alias {trainers}
{
	#if {&trainers == 0}
	{
		#showme { No trainers recorded yet! };
		#return;
	};
	#format {query} {%n} {%0};
	#format {query_length} {%L} {$query};
	#if {$query_length == 0}
	{
		#showme {Known trainable skills:};
		#foreach {$trainers[]} {skill}
		{
			#showme {   $trainers[$skill][color]$skill}
		};
		#return;
	};
	#if {&trainers[$query][] > 0}
	{
		#showme {$query is taught by:};
		#foreach {$trainers[$query][teachers][]} {trainer}
		{
			#showme {   $trainer for $trainers[$query][teachers][$trainer]};
		};
	}
	{
		#showme {No known trainers for "$query"};
	};
};
